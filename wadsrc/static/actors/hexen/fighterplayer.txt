// The fighter --------------------------------------------------------------

ACTOR FighterPlayer : PlayerPawn
{
	Health 100
	PainChance 255
	Radius 16
	Height 64
	Speed 1
	+NOSKIN
	PainSound "PlayerFighterPain"
	RadiusDamageFactor 0.25
	Player.JumpZ 9.75
	Player.Viewheight 48
	Player.ColorRange 246, 254
	Player.SpawnClass "Fighter"
	Player.DisplayName "Fighter"
	Player.SoundClass "fighter"
	Player.ScoreIcon "FITEFACE"
	Player.HealRadiusType "Armor"
	Player.Hexenarmor 15, 25, 20, 15, 5
	Player.StartItem "FWeapFist"
	Player.ForwardMove 1.08, 1.2
	Player.SideMove 1.125, 1.475
	Player.WeaponSlot 1, FWeapFist
	Player.WeaponSlot 2, FWeapAxe
	Player.WeaponSlot 3, FWeapHammer
	Player.WeaponSlot 4, FWeapQuietus
	
	States
	{
	Spawn:
		PLAY A -1
		Stop
	See:
		PLAY ABCD 4
		Loop
	Missile:
	Melee:
		PLAY EF 8
		Goto Spawn
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		Goto Spawn
	Death:
		PLAY H 6
		PLAY I 6 A_PlayerScream
		PLAY JK 6
		PLAY L 6 A_NoBlocking
		PLAY M 6
		PLAY N -1
		Stop		
	XDeath:
		PLAY O 5 A_PlayerScream
		PLAY P 5 A_SkullPop
		PLAY R 5 A_NoBlocking
		PLAY STUV 5
		PLAY W -1
		Stop
	Ice:
		PLAY X 5 A_FreezeDeath
		PLAY X 1 A_FreezeDeathChunks
		Wait
	Burn:
		FDTH A 5 BRIGHT A_PlaySound("*burndeath")
		FDTH B 4 BRIGHT
		FDTH G 5 BRIGHT
		FDTH H 4 BRIGHT A_PlayerScream
		FDTH I 5 BRIGHT
		FDTH J 4 BRIGHT
		FDTH K 5 BRIGHT
		FDTH L 4 BRIGHT
		FDTH M 5 BRIGHT
		FDTH N 4 BRIGHT
		FDTH O 5 BRIGHT
		FDTH P 4 BRIGHT
		FDTH Q 5 BRIGHT
		FDTH R 4 BRIGHT
		FDTH S 5 BRIGHT A_NoBlocking
		FDTH T 4 BRIGHT
		FDTH U 5 BRIGHT
		FDTH V 4 BRIGHT
		ACLO E 35 A_CheckPlayerDone
		Wait
		ACLO E 8
		Stop
	}
}

