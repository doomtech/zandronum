
// Base class for the beggars ---------------------------------------------

ACTOR Beggar : StrifeHumanoid
{
	Health 20
	PainChance 250
	Speed 3
	Radius 20
	Height 56
	Monster
	+JUSTHIT
	-COUNTKILL
	+NOSPLASHALERT
	MinMissileChance 150
	Tag "$TAG_BEGGAR"
	MaxStepHeight 16
	MaxDropoffHeight 32
	HitObituary "$OB_BEGGAR"

	AttackSound "beggar/attack"
	PainSound "beggar/pain"
	DeathSound "beggar/death"
	States
	{
	Spawn:
		BEGR A 10 A_Look
		Loop
	See:
		BEGR AABBCC 4 A_Wander
		Loop
	Melee:
		BEGR D 8
		BEGR D 8 A_CustomMeleeAttack(2*random[PeasantAttack](1,5)+2)
		BEGR E 1 A_Chase
		BEGR D 8 A_SentinelRefire
		Loop
	Pain:
		BEGR A 3 A_Pain
		BEGR A 3 A_Chase
		Goto Melee
	Death:
		BEGR F 4
		BEGR G 4 A_Scream
		BEGR H 4
		BEGR I 4 A_NoBlocking
		BEGR JKLM 4
		BEGR N -1
		Stop
	XDeath:
		BEGR F 5 A_TossGib
		GIBS M 5 A_TossGib
		GIBS N 5 A_XScream
		GIBS O 5 A_NoBlocking
		GIBS PQRST 4 A_TossGib
		GIBS U 5
		GIBS V 1400
		Stop
	}
}
			

// Beggars -----------------------------------------------------------------

ACTOR Beggar1 : Beggar 141
{
	Game Strife
	ConversationID 38, 37, 38
}


ACTOR Beggar2 : Beggar 155
{
	Game Strife
	ConversationID 39, 38, 39
}


ACTOR Beggar3 : Beggar 156
{
	Game Strife
	ConversationID 40, 39, 40
}


ACTOR Beggar4 : Beggar 157
{
	Game Strife
	ConversationID 41, 40, 41
}


ACTOR Beggar5 : Beggar 158
{
	Game Strife
	ConversationID 42, 41, 42
}
