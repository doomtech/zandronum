// --------------------------------------------------------------------------
//
// Doom weapon base class
//
// --------------------------------------------------------------------------

// --------------------------------------------------------------------------
//
// Fist 
//
// --------------------------------------------------------------------------

ACTOR Fist : Weapon
{
	Weapon.SelectionOrder 3700
	Weapon.Kickback 100
	Obituary "$OB_MPFIST"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		PUNG A 1 A_Lower
		Loop
	Select:
		PUNG A 1 A_Raise
		Loop
	Fire:
		PUNG B 4
		PUNG C 4 A_Punch
		PUNG D 5
		PUNG C 4
		PUNG B 5 A_ReFire
		Goto Ready
	}
}
	

// --------------------------------------------------------------------------
//
// Chainsaw
//
// --------------------------------------------------------------------------

ACTOR Chainsaw : Weapon 2005
{
	Game Doom
	SpawnID 32
	Weapon.Kickback 0
	Weapon.SelectionOrder 2200
	Weapon.UpSound "weapons/sawup"
	Weapon.ReadySound "weapons/sawidle"
	Inventory.PickupMessage "$GOTCHAINSAW"
	Obituary "$OB_MPCHAINSAW"
	+WEAPON.MELEEWEAPON		
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	States
	{
	Ready:
		SAWG CD 4 A_WeaponReady
		Loop
	Deselect:
		SAWG C 1 A_Lower
		Loop
	Select:
		SAWG C 1 A_Raise
		Loop
	Fire:
		SAWG AB 4 A_Saw
		SAWG B 0 A_ReFire
		Goto Ready
	Spawn:
		CSAW A -1
		Stop
	}
}


// --------------------------------------------------------------------------
//
// Pistol
//
// --------------------------------------------------------------------------

ACTOR Pistol : Weapon 5010
{
	Weapon.Kickback 100
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Obituary "$OB_MPPISTOL"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Radius 20
	Height 16
	Attacksound "weapons/pistol"
	States
	{
 	Spawn:
		PIST A -1
		Stop
	Ready:
		PISG A 1 A_WeaponReady
		Loop
	Deselect:
		PISG A 1 A_Lower
		Loop
	Select:
		PISG A 1 A_Raise
		Loop
	Fire:
		PISG A 4
		PISG B 0 A_FireBullets (5.6, 0, 1, 5, "BulletPuff")
		PISG B 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	Flash:
		PISF A 7 bright A_Light1
		PISF A 0 bright A_Light0
		Stop
 	}
}


// --------------------------------------------------------------------------
//
// Shotgun
//
// --------------------------------------------------------------------------

ACTOR Shotgun : Weapon 2001
{
	SpawnID 27
	Obituary "$OB_MPSHOTGUN"
	Radius 20
	Height 16
	Attacksound "weapons/shotgf"
	Inventory.Pickupmessage "$GOTSHOTGUN"
	Weapon.SelectionOrder 1300
	Weapon.Kickback 100
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	States
	{
		Ready:
		SHTG A 1 A_WeaponReady
		Loop
	Deselect: 
		SHTG A 1 A_Lower
		Loop
	Select:
		SHTG A 1 A_Raise
		Loop
	Fire:
		SHTG A 3
		// Next 2 states replace A_FireShotgun
		SHTG A 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
		SHTG A 7 A_GunFlash
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 4 bright A_Light1
		SHTF B 3 bright A_Light2
		SHTF B 0 bright A_Light0
		Stop
	Spawn:
		SHOT A -1
		Stop
	}
}

// --------------------------------------------------------------------------
//
// Chaingun
//
// --------------------------------------------------------------------------

ACTOR Chaingun : Weapon 2002
{
	SpawnID 28
	Obituary "$OB_MPCHAINGUN"
	Radius 20
	Height 16
	Attacksound "weapons/chngun"
	Inventory.Pickupmessage "$GOTCHAINGUN"
	Weapon.SelectionOrder 700
	Weapon.Kickback 100
	Weapon.AmmoType "Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	States
	{
	Ready:
		CHGG A 1 A_WeaponReady
		Loop
	Deselect: 
		CHGG A 1 A_Lower
		Loop
	Select:
		CHGG A 1 A_Raise
		Loop
	Fire:
		CHGG A 0 A_FireBullets (5.6, 0, 1, 5, "BulletPuff")
		CHGG A 4 A_GunFlash
		CHGG B 4 A_FireBullets (5.6, 0, 1, 5, "BulletPuff")
		CHGG B 0 A_ReFire
		Goto Ready
	Flash:
		CHGF A 4 bright A_Light1
		CHGF B 5 bright A_Light2
		CHGF A 0 bright A_Light0
		Stop
	Spawn:
		MGUN A -1
		Stop
	}
}